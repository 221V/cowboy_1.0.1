<!DOCTYPE html>
<html>
<head>
<title>The Req object</title>
<meta charset="utf8">
<style>
div.layout{width:800px;margin:0 auto;}
</style>
<link rel="stylesheet" href="slides.css" type="text/css">
</head>
<body>
<div class="layout">
<h1>The Req object</h1>

<p>The Req object is this variable that you will use to obtain information about a request, read the body of the request and send a response.</p>

<h2>A special variable</h2>

<p>While we call it an "object", it is not an object in the OOP sense of the term. In fact it is completely opaque to you and the only way you can perform operations using it is by calling the functions from the <code>cowboy_req</code> module.</p>

<p>Almost all the calls to the <code>cowboy_req</code> module will return an updated request object. Just like you would keep the updated <code>State</code> variable in a gen_server, you MUST keep the updated <code>Req</code> variable in a Cowboy handler. Cowboy will use this object to know whether a response has been sent when the handler has finished executing.</p>

<p>The Req object allows accessing both immutable and mutable state. This means that calling some of the functions twice will not produce the same result. For example, when streaming the request body, the function will return the body by chunks, one at a time, until there is none left.</p>

<p>It also caches the result of operations performed on the immutable state. That means that some calls will give a result much faster when called many times.</p>

<h2>Overview of the cowboy_req interface</h2>

<p>The <code>cowboy_req</code> interface is divided in four groups of functions, each having a well defined return type signature common to the entire group.</p>

<p>The first group, access functions, will always return <code>{Value, Req}</code>. The group includes all the following functions: <code>binding/{2,3}</code>, <code>bindings/1</code>, <code>body_length/1</code>, <code>cookie/{2,3}</code>, <code>cookies/1</code>, <code>header/{2,3}</code>, <code>headers/1</code>, <code>host/1</code>, <code>host_info/1</code>, <code>host_url/1</code>, <code>meta/{2,3}</code>, <code>method/1</code>, <code>path/1</code>, <code>path_info/1</code>, <code>peer/1</code>, <code>port/1</code>, <code>qs/1</code>, <code>qs_val/{2,3}</code>, <code>qs_vals/1</code>, <code>url/1</code>, <code>version/1</code>.</p>

<p>The second group, question functions, will always return a <code>boolean()</code>. The group includes the following three functions: <code>has_body/1</code>, <code>has_resp_body/1</code>, <code>has_resp_header/2</code>.</p>

<p>The third group contains the functions that manipulate the socket or perform operations that may legitimately fail. They may return <code>{Result, Req}</code>, <code>{Result, Value, Req}</code> or <code>{error, atom()}</code>. This includes the following functions: <code>body/{1,2}</code>, <code>body_qs/{1,2}</code>, <code>chunked_reply/{2,3}</code>, <code>parse_header/{2,3}</code>, <code>part/{1,2}</code>, <code>part_body/{1,2}</code> and <code>reply/{2,3,4}</code>. Finally, the group also includes the <code>chunk/2</code> and <code>continue/1</code> functions which always return <code>ok</code>.</p>

<p>The final group modifies the Req object state without performing any immediate operations. As these functions can't fail, they always return a new <code>Req</code> directly. This includes the following functions: <code>compact/1</code>, <code>delete_resp_header/2</code>, <code>set_meta/3</code>, <code>set_resp_body/2</code>, <code>set_resp_body_fun/{2,3}</code>, <code>set_resp_cookie/4</code>, <code>set_resp_header/3</code>.</p>

<p>This chapter covers most of the first group, plus a few other functions. The next few chapters cover cookies handling, reading the request body and sending a response.</p>

<h2>Request</h2>

<p>When a client performs a request, it first sends a few required values. They are sent differently depending on the protocol being used, but the intent is the same. They indicate to the server the type of action it wants to do and how to locate the resource to perform it on.</p>

<p>The method identifies the action. Standard methods include GET, HEAD, OPTIONS, PATCH, POST, PUT, DELETE. Method names are case sensitive.</p>

<pre>
{Method, Req2} = cowboy_req:method(Req).
</pre>

<p>The host, port and path parts of the URL identify the resource being accessed. The host and port information may not be available if the client uses HTTP/1.0.</p>

<pre>
{Host, Req2} = cowboy_req:host(Req),
{Port, Req3} = cowboy_req:port(Req2),
{Path, Req4} = cowboy_req:path(Req3).
</pre>

<p>The version used by the client can of course also be obtained.</p>

<pre>
{Version, Req2} = cowboy_req:version(Req).
</pre>

<p>Do note however that clients claiming to implement one version of the protocol does not mean they implement it fully, or even properly.</p>

<h2>Bindings</h2>

<p>After routing the request, bindings are available. Bindings are these parts of the host or path that you chose to extract when defining the routes of your application.</p>

<p>You can fetch a single binding. The value will be <code>undefined</code> if the binding doesn't exist.</p>

<pre>
{Binding, Req2} = cowboy_req:binding(my_binding, Req).
</pre>

<p>If you need a different value when the binding doesn't exist, you can change the default.</p>

<pre>
{Binding, Req2} = cowboy_req:binding(my_binding, Req, 42).
</pre>

<p>You can also obtain all bindings in one call. They will be returned as a list of key/value tuples.</p>

<pre>
{AllBindings, Req2} = cowboy_req:bindings(Req).
</pre>

<p>If you used <code>...</code> at the beginning of the route's pattern for the host, you can retrieve the matched part of the host. The value will be <code>undefined</code> otherwise.</p>

<pre>
{HostInfo, Req2} = cowboy_req:host_info(Req).
</pre>

<p>Similarly, if you used <code>...</code> at the end of the route's pattern for the path, you can retrieve the matched part, or get <code>undefined</code> otherwise.</p>

<pre>
{PathInfo, Req2} = cowboy_req:path_info(Req).
</pre>

<h2>Query string</h2>

<p>The query string can be obtained directly.</p>

<pre>
{Qs, Req2} = cowboy_req:qs(Req).
</pre>

<p>You can also requests only one value.</p>

<pre>
{QsVal, Req2} = cowboy_req:qs_val(<<"lang">>, Req).
</pre>

<p>If that value is optional, you can define a default to simplify your task.</p>

<pre>
{QsVal, Req2} = cowboy_req:qs_val(<<"lang">>, Req, <<"en">>).
</pre>

<p>Finally, you can obtain all query string values.</p>

<pre>
{AllValues, Req2} = cowboy_req:qs_vals(Req).
</pre>

<h2>Request URL</h2>

<p>You can reconstruct the full URL of the resource.</p>

<pre>
{URL, Req2} = cowboy_req:url(Req).
</pre>

<p>You can also obtain only the base of the URL, excluding the path and query string.</p>

<pre>
{BaseURL, Req2} = cowboy_req:host_url(Req).
</pre>

<h2>Headers</h2>

<p>Cowboy allows you to obtain the header values as string, or parsed into a more meaningful representation.</p>

<p>This will get the string value of a header.</p>

<pre>
{HeaderVal, Req2} = cowboy_req:header(<<"content-type">>, Req).
</pre>

<p>You can of course set a default in case the header is missing.</p>

<pre>
{HeaderVal, Req2}
    = cowboy_req:header(<<"content-type">>, Req, <<"text/plain">>).
</pre>

<p>And also obtain all headers.</p>

<pre>
{AllHeaders, Req2} = cowboy_req:headers(Req).
</pre>

<p>To parse the previous header, simply call <code>parse_header/{2,3}</code> where you would call <code>header/{2,3}</code> otherwise. Note that the return value changes and includes the result of the operation as the first element of the returned tuple. A successful parse returns <code>ok</code>.</p>

<pre>
{ok, ParsedVal, Req2} = cowboy_req:parse_header(<<"content-type">>, Req).
</pre>

<p>When Cowboy doesn't know how to parse the given header, the result of the operation will be <code>undefined</code> and the string value will be returned instead.</p>

<pre>
{undefined, HeaderVal, Req2}
    = cowboy_req:parse_header(<<"unicorn-header">>, Req).
</pre>

<p>When parsing fails, <code>{error, Reason}</code> is returned instead.</p>

<p>You can of course define a default value. Note that the default value you specify here is the parsed value you'd like to get by default.</p>

<pre>
{ok, ParsedVal, Req2}
    = cowboy_req:parse_header(<<"content-type">>, Req,
    {<<"text">>, <<"plain">>, []}).
</pre>

<p>The list of known headers and default values is defined in the manual. Also note that the result of parsing is cached, so calling this function multiple times for the same values will not have a significant performance impact.</p>

<h2>Meta</h2>

<p>Cowboy will sometimes associate some meta information with the request. Built-in meta values are listed in the manual for their respective modules.</p>

<p>This will get a meta value. The returned value will be <code>undefined</code> if it isn't defined.</p>

<pre>
{MetaVal, Req2} = cowboy_req:meta(websocket_version, Req).
</pre>

<p>You can change the default value if needed.</p>

<pre>
{MetaVal, Req2} = cowboy_req:meta(websocket_version, Req, 13).
</pre>

<p>You can also define your own meta values. The name must be an <code>atom()</code>.</p>

<pre>
Req2 = cowboy_req:set_meta(the_answer, 42, Req).
</pre>

<h2>Peer</h2>

<p>You can obtain the peer address and port number. This is not necessarily the actual IP and port of the client, but rather the one of the machine that connected to the server.</p>

<pre>
{{IP, Port}, Req2} = cowboy_req:peer(Req).
</pre>

<h2>Reducing the memory footprint</h2>

<p>When you are done reading information from the request object and know you are not going to access it anymore, for example when using long-polling or Websocket, you can use the <code>compact/1</code> function to remove most of the data from the request object and free memory.</p>

<pre>
Req2 = cowboy_req:compact(Req).
</pre>

<p>You will still be able to send a reply if needed.</p>
</div>
<script>SyntaxHighlighter.all();</script>
</body>
</html>
