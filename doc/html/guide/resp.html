<!DOCTYPE html>
<html>
<head>
<title>Sending a response</title>
<meta charset="utf8">
<style>
div.layout{width:800px;margin:0 auto;}
</style>
<link rel="stylesheet" href="slides.css" type="text/css">
</head>
<body>
<div class="layout">
<h1>Sending a response</h1>

<p>The Req object also allows you to send a response.</p>

<p>You can only send one response. Any other attempt will trigger a crash. The response may be sent in one go or with its body streamed by chunks of arbitrary size.</p>

<p>You can also set headers or the response body in advance and Cowboy will use them when you finally do reply.</p>

<h2>Reply</h2>

<p>You can send a reply with no particular headers or body. Cowboy will make sure to send the mandatory headers with the response.</p>

<pre>
{ok, Req2} = cowboy_req:reply(200, Req).
</pre>

<p>You can define headers to be sent with the response. Note that header names must be lowercase. Again, Cowboy will make sure to send the mandatory headers with the response.</p>

<pre>
{ok, Req2} = cowboy_req:reply(303, [
    {<<"location">>, <<"http://ninenines.eu">>}
], Req).
</pre>

<p>You can override headers that Cowboy would send otherwise. Any header set by the user will be used over the ones set by Cowboy. For example, you can advertise yourself as a different server.</p>

<pre>
{ok, Req2} = cowboy_req:reply(200, [
    {<<"server">>, <<"yaws">>}
], Req).
</pre>

<p>We also saw earlier how to force close the connection by overriding the connection header.</p>

<p>Finally, you can also send a body with the response. Cowboy will automatically set the content-length header if you do. We recommend that you set the content-type header so the client may know how to read the body.</p>

<pre>
{ok, Req2} = cowboy_req:reply(200, [
    {<<"content-type">>, <<"text/plain">>}
], "Hello world!", Req).
</pre>

<p>Here is the same example but sending HTML this time.</p>

<pre>
{ok, Req2} = cowboy_req:reply(200, [
    {<<"content-type">>, <<"text/html">>}
], "<html><head>Hello world!</head><body><p>Hats off!</p></body></html>", Req).
</pre>

<p>Note that the reply is sent immediately.</p>

<h2>Chunked reply</h2>

<p>You can also stream the response body. First, you need to initiate the reply by sending the response status code. Then you can send the body in chunks of arbitrary size.</p>

<pre>
{ok, Req2} = cowboy_req:chunked_reply(200, Req),
ok = cowboy_req:chunk("Hello...", Req2),
ok = cowboy_req:chunk("chunked...", Req2),
ok = cowboy_req:chunk("world!!", Req2).
</pre>

<p>You should make sure to match on <code>ok</code> as an error may be returned.</p>

<p>While it is possible to send a chunked response without a content-type header, it is still recommended. You can set this header or any other just like for normal replies.</p>

<pre>
{ok, Req2} = cowboy_req:chunked_reply(200, [
    {<<"content-type">>, <<"text/html">>}
], Req),
ok = cowboy_req:chunk("<html><head>Hello world!</head>", Req2),
ok = cowboy_req:chunk("<body><p>Hats off!</p></body></html>", Req2).
</pre>

<p>Note that the reply and each chunk following it are sent immediately.</p>

<h2>Preset response headers</h2>

<p>You can define response headers in advance. They will be merged into the headers given in the reply call. Headers in the reply call override preset response headers which override the default Cowboy headers.</p>

<pre>
Req2 = cowboy_req:set_resp_header(<<"allow">>, "GET", Req).
</pre>

<p>You can check if a response header has already been set. This will only check the response headers that you set, and not the ones Cowboy will add when actually sending the reply.</p>

<pre>
cowboy_req:has_resp_header(<<"allow">>, Req).
</pre>

<p>It will return <code>true</code> if the header is defined, and <code>false</code> otherwise.</p>

<p>Finally, you can also delete a preset response header if needed. If you do, it will not be sent.</p>

<pre>
Req2 = cowboy_req:delete_resp_header(<<"allow">>, Req).
</pre>

<h2>Preset response body</h2>

<p>You can set the response body in advance. Note that this body will be ignored if you then choose to send a chunked reply, or if you send a reply with an explicit body.</p>

<pre>
Req2 = cowboy_req:set_resp_body("Hello world!", Req).
</pre>

<p>You can also set a fun that will be called when it is time to send the body. There are three different ways of doing that.</p>

<p>If you know the length of the body that needs to be sent, you should specify it, as it will help clients determine the remaining download time and allow them to inform the user.</p>

<pre>
F = fun (Socket, Transport) ->
    Transport:send(Socket, "Hello world!")
end,
Req2 = cowboy_req:set_resp_body_fun(12, F, Req).
</pre>

<p>If you do not know the length of the body, you should use a chunked response body fun instead.</p>

<pre>
F = fun (SendChunk) ->
    Body = lists:duplicate(random:uniform(1024, $a)),
    SendChunk(Body)
end,
Req2 = cowboy_req:set_resp_body_fun(chunked, F, Req).
</pre>

<p>Finally, you can also send data on the socket directly, without knowing the length in advance. Cowboy may be forced to close the connection at the end of the response though depending on the protocol capabilities.</p>

<pre>
F = fun (Socket, Transport) ->
    Body = lists:duplicate(random:uniform(1024, $a)),
    Transport:send(Socket, Body)
end,
Req2 = cowboy_req:set_resp_body_fun(F, Req).
</pre>

<h2>Sending files</h2>

<p>You can send files directly from disk without having to read them. Cowboy will use the <code>sendfile</code> syscall when possible, which means that the file is sent to the socket directly from the kernel, which is a lot more performant than doing it from userland.</p>

<p>Again, it is recommended to set the size of the file if it can be known in advance.</p>

<pre>
F = fun (Socket, Transport) ->
    Transport:sendfile(Socket, "priv/styles.css")
end,
Req2 = cowboy_req:set_resp_body_fun(FileSize, F, Req).
</pre>

<p>Please see the Ranch guide for more information about sending files.</p>
</div>
<script>SyntaxHighlighter.all();</script>
</body>
</html>
