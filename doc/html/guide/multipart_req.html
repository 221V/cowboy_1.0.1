<!DOCTYPE html>
<html>
<head>
<title>Multipart requests</title>
<meta charset="utf8">
<style>
div.layout{width:800px;margin:0 auto;}
</style>
<link rel="stylesheet" href="slides.css" type="text/css">
</head>
<body>
<div class="layout">
<h1>Multipart requests</h1>

<p>You can read and parse multipart messages using the Req object directly.</p>

<p>Cowboy defines two functions that allows you to get information about each part and read their contents.</p>

<h2>Checking the content-type</h2>

<p>While there is a variety of multipart messages, the most common on the Web is <code>multipart/form-data</code>. It's the type of message being sent when an HTML form allows uploading files.</p>

<p>You can quickly figure out if a multipart message has been sent by parsing the <code>content-type</code> header.</p>

<pre>
{ok, {<<"multipart">>, <<"form-data">>, _}, Req2}
    = cowboy_req:parse_header(<<"content-type">>, Req).
</pre>

<h2>Reading a multipart message</h2>

<p>To read a message you have to iterate over all its parts. Then, for each part, you can inspect its headers and read its body.</p>

<pre>
multipart(Req) ->
    case cowboy_req:part(Req) of
        {ok, _Headers, Req2} ->
            {ok, _Body, Req3} = cowboy_req:part_body(Req2),
            multipart(Req3);
        {done, Req2} ->
            Req2
    end.
</pre>

<p>Parts do not have a size limit. When a part body is too big, Cowboy will return what it read so far and allow you to continue if you wish to do so.</p>

<p>The function <code>cow_multipart:form_data/1</code> can be used to quickly obtain information about a part from a <code>multipart/form-data</code> message. This function will tell you if the part is for a normal field or if it is a file being uploaded.</p>

<p>This can be used for example to allow large part bodies for files but crash when a normal field is too large.</p>

<pre>
multipart(Req) ->
    case cowboy_req:part(Req) of
        {ok, Headers, Req2} ->
            Req4 = case cow_multipart:form_data(Headers) of
                {data, _FieldName} ->
                    {ok, _Body, Req3} = cowboy_req:part_body(Req2),
                    Req3;
                {file, _FieldName, _Filename, _CType, _CTransferEncoding} ->
                    stream_file(Req2)
            end,
            multipart(Req4);
        {done, Req2} ->
            Req2
    end.

stream_file(Req) ->
    case cowboy_req:part_body(Req) of
        {ok, _Body, Req2} ->
            Req2;
        {more, _Body, Req2} ->
            stream_file(Req2)
    end.
</pre>

<p>By default the body chunk Cowboy will return is limited to 8MB. This can of course be overriden. Both functions can take a second argument, the same list of options that will be passed to <code>cowboy_req:body/2</code> function.</p>

<h2>Skipping unwanted parts</h2>

<p>If you do not want to read a part's body, you can skip it. Skipping is easy. If you do not call the function to read the part's body, Cowboy will automatically skip it when you request the next part.</p>

<p>The following snippet reads all part headers and skips all bodies:</p>

<pre>
multipart(Req) ->
    case cowboy_req:part(Req) of
        {ok, _Headers, Req2} ->
            multipart(Req2);
        {done, Req2} ->
            Req2
    end.
</pre>

<p>Similarly, if you start reading the body and it ends up being too big, you can simply continue with the next part, Cowboy will automatically skip what remains.</p>

<p>Note that the skipping rate may not be adequate for your application. If you observe poor performance when skipping, you might want to consider manually skipping by calling the <code>cowboy_req:part_body/1</code> function directly.</p>

<p>And if you started reading the message but decide that you do not need the remaining parts, you can simply stop reading entirely and Cowboy will automatically figure out what to do.</p>
</div>
<script>SyntaxHighlighter.all();</script>
</body>
</html>
